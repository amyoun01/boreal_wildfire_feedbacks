# Response: Gamma
# Parameters_to_save: beta0 beta1 beta2 shape aab_init offset

model {

  # ----------------------------------------------------------------------------
  # Regression priors
  # ----------------------------------------------------------------------------
  beta0 ~ dnorm(0, 1 / (10^2))
  beta1 ~ dnorm(0, 1 / (10^2))
  beta2 ~ dnorm(0, 1 / (10^2))

  # ----------------------------------------------------------------------------
  # Shape parameter prior
  # ----------------------------------------------------------------------------
  shape ~ dexp(0.1)

  # ----------------------------------------------------------------------------
  # Initialize 60-yr fire history for first year in time series (1980)
  # ----------------------------------------------------------------------------
  # Distribution of mean aab (1980-1999)
  aab_init ~ dlnorm(aab_meanlog, 1 / (aab_selog^2))
  aab_hist <- rep(aab_init, 60) # For 60 years prior

  # ----------------------------------------------------------------------------
  # Initiailze and run first iteration for likelihood outside of for loop.
  # ----------------------------------------------------------------------------

  R[1] <- sum(w * aab_hist)
  M[1] <- S - R[1]
  offset[1] <- log(M[1])

  # Use log-link
  log(mu[1]) <- beta0 + beta1 * BUI[1] + beta2 * ISI[1] + 1 * offset[1]

  rate[1] <- shape / mu[1]

  aab[1] ~ dgamma(shape, rate[1])

  # ----------------------------------------------------------------------------
  # Likelihood
  # ----------------------------------------------------------------------------

  for (i in 2:n) {

    R[i] <- sum(w * c(aab_hist[i:60], aab[1:(i-1)]))
    M[i] <- ifelse(S > R[i], S - R[i], 1)
    offset[i] <- log(M[i])

    # Use natural logarithm to link linear predictor to Gamma distribution
    log(mu[i]) <- beta0 + beta1 * BUI[i] + beta2 * ISI[i] + 1 * offset[i]

    # Estimate rate parameter from shape and mu
    rate[i] <- shape / mu[i]

    aab[i] ~ dgamma(shape, rate[i])

  }

}
